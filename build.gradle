plugins {
	id "application"

	id "io.freefair.lombok"

	id "net.ltgt.errorprone"
	id "net.ltgt.nullaway"

	id "se.patrikerdes.use-latest-versions"

	id "com.diffplug.spotless"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.guava:guava:_"
	testImplementation Testing.junit.jupiter
	errorprone "com.google.errorprone:error_prone_core:_"
	errorprone "com.uber.nullaway:nullaway:_"
}

nullaway {
	annotatedPackages.add("io.github.rami3l.template.java")
}

spotless {
	java {
		googleJavaFormat()
	}
	groovyGradle {
		target "*.gradle"
		greclipse()
	}
}

application {
	// Define the main class for the application.
	mainClass = "io.github.rami3l.template.java.App"
}

compileJava {
	options.compilerArgs += ["--enable-preview"]
}

compileTestJava {
	options.compilerArgs += ["--enable-preview"]
}

test {
	jvmArgs "--enable-preview"
	useJUnitPlatform()
}
